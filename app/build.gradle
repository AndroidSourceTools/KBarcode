apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply from: "$project.rootDir/config/quality/quality.gradle"
apply from: "$project.rootDir/config/misc/apknaming.gradle"

allOpen {
    annotation 'uk.co.brightec.kbarcode.app.util.OpenClass'
}

import java.text.SimpleDateFormat

static def buildTime() {
    def df = new SimpleDateFormat("dd.MM.yyyy HH.mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC+01:00"))
    return df.format(new Date())
}

static def getBuildNumber() {
    return System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 1
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "uk.co.brightec.kbarcode.app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getBuildNumber()
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        lintConfig file("$project.rootDir/config/quality/lint.xml")
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/config/proguard-rules.pro"
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/config/proguardTest-rules.pro"
            versionNameSuffix " (DEBUG ${buildTime()})"
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/config/proguard-rules.pro"
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/config/proguardTest-rules.pro"
            versionNameSuffix " (#${getBuildNumber()})"
        }
    }
}

dependencies {
    // Use this in your app
//    implementation 'uk.co.brightec.kbarcode:kbarcode:$version'
    // We use this for developing the library
    implementation project(':kbarcode')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    //region Local Unit Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.24.5'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    //endregion

    //region Instrumented Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'org.mockito:mockito-android:2.24.5'
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    // UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    //endregion
}

apply plugin: 'com.google.gms.google-services'
